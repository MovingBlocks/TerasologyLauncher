// Copyright 2020 The Terasology Foundation
// SPDX-License-Identifier: Apache-2.0

/*
 * This is a Gradle build file:
 * - Gradle Homepage: http://gradle.org/
 * - Gradle Documentation: http://gradle.org/documentation
 * - View tasks for this project: $ gradlew tasks
 */

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        gradlePluginPortal()
    }
}

plugins {
    id 'application'
    id 'checkstyle'
    id "com.github.spotbugs" version "4.0.4"
    id 'de.undercouch.download' version '4.0.4'
    id 'java'
    // to help clarify conflicting logging providers
    id 'name.remal.component-metadata' version '1.0.186'
    id 'nebula.release' version '14.1.0'
    id 'pmd'
    id 'project-report'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id "org.jetbrains.gradle.plugin.idea-ext" version "0.9"
}

apply plugin: 'org.terasology.gradlegoo'
apply from: "./config/gradle/jre.gradle"

// Test for right version of Java in use for running this script
assert JavaVersion.current().isJava11Compatible()

import org.apache.tools.ant.filters.FixCrLfFilter

import java.text.SimpleDateFormat

def dateTimeFormat = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ssXXX")
dateTimeFormat.timeZone = TimeZone.getTimeZone("UTC")

configurations {
    codeMetrics
}

// Declare "extra properties" (variables) for the project - a Gradle thing that makes them special.
ext {
    // Read environment variables, including variables passed by jenkins continuous integration server
    env = System.getenv()

    // Stuff for our automatic version file setup
    startDateTimeString = dateTimeFormat.format(new Date())

    // Splash image for the JAR
    splashImage = 'org/terasology/launcher/images/splash.jpg'

    // Shared code analytics configurations via retrieved config zip
    metricsConfigDir = 'config/metrics'
}

// Declare remote repositories we're interested in - library files will be fetched from here
repositories {
    // Main Maven repo
    mavenCentral()
    jcenter()
    maven {
        // MovingBlocks Artifactory instance(s) for libs not readily available elsewhere plus our own libs
        name "Terasology Artifactory"
        url "http://artifactory.terasology.org/artifactory/virtual-repo-live"
        allowInsecureProtocol true
    }
    maven {
        // for spf4j dependencies that haven't been merged upstream
        // i.e. org.apache.avro:avro
        name "spf4j dependencies"
        url "https://dl.bintray.com/zolyfarkas/core"
    }
    maven {
        name "JitPack"  // used by org.everit.json.schema
        url "https://jitpack.io"
    }
}

// Primary dependencies definition
dependencies {
    implementation("org.slf4j:slf4j-api:[1.7.+, 2.0.0-alpha1]") {
        because "influenced by app or test loggers as needed"
    }
    implementation("ch.qos.logback:logback-classic:1.3.0-alpha5") {
        because "1.3 series uses ServiceLoader (more packaging friendly?)"
    }

    implementation group: 'com.atlassian.commonmark', name: 'commonmark', version: '0.14.0'

    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
    implementation group: "com.google.guava", name: "guava", version: "28.1-jre"
    implementation group: 'com.github.everit-org.json-schema', name: 'org.everit.json.schema', version: '1.12.1'

    implementation group: 'org.kohsuke', name: 'github-api', version: '1.111'

    implementation group: 'com.vdurmont', name: 'semver4j', version: '3.1.0'

    // These dependencies are only needed for running tests

    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.+"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.6.+"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.6.+"

    testImplementation('org.mockito:mockito-inline:3.4.+') {
        because "-inline build enables mocking final classes"
        // https://javadoc.io/doc/org.mockito/mockito-core/latest/org/mockito/Mockito.html#0.2
        // > Be aware that this artifact may be abolished when the inline mock making feature is integrated into the default mock maker.
    }
    testImplementation "org.mockito:mockito-junit-jupiter:3.4.+"

    testImplementation('org.spf4j:spf4j-slf4j-test:8.8.5') {
        because "testable logging"
    }
    testImplementation("org.slf4j:slf4j-api:1.7.+!!") {
        because "spf4j-slf4j-test:8.8 needs slf4j-api:1.x"
    }

    testImplementation("org.testfx:testfx-core:4.0.16-alpha") {
        because "to test JavaFX Application"
        // -alpha because that's the only kind of release they have?
    }
    testImplementation "org.testfx:testfx-junit5:4.0.16-alpha"

    testImplementation("org.testfx:openjfx-monocle:jdk-12.0.1+2") {
        // nobody's uploaded a jdk-14 build yet. does the jdk-12 one work?
        because "CI builders are headless environments"
    }

    // Config for our code analytics from: https://github.com/MovingBlocks/TeraConfig
    codeMetrics group: 'org.terasology.config', name: 'codemetrics', version: '1.5.1', ext: 'zip'
}

configurations.matching({ it =~ ~/test(Runtime|Compile|Implementation)Classpath/ }).all {
    resolutionStrategy(gradlegoo.prefers("logging", "jcl-api-capability", "jcl-over-slf4j",
            "jcl should prefer slf4j when available"))
    resolutionStrategy(gradlegoo.prefers("logging", "slf4j-impl-capability",
            "spf4j-slf4j-test", "tests use slf4j-test"))

    // old hamcrest versions exist only to spite us
    // http://hamcrest.org/JavaHamcrest/distributables#upgrading-from-hamcrest-1x
    exclude group: "org.hamcrest", module: "hamcrest-core"
    exclude group: "org.hamcrest", module: "hamcrest-library"
}

// Set the expected module Java level (can use a higher Java to run, but should not use features from a higher Java)
sourceCompatibility = 1.11
targetCompatibility = 1.11

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

javafx {
    version = "12.0.1"
    modules = [
            'javafx.graphics',
            'javafx.fxml',
            'javafx.web'
    ]
}

test {
    useJUnitPlatform()
}

checkstyle {
    ignoreFailures = false
    configFile = new File("$rootDir/config/metrics/checkstyle/checkstyle.xml")
    configProperties.samedir = checkstyle.configFile.parentFile
}

pmd {
    ignoreFailures = true
    ruleSetFiles = files("$rootDir/config/metrics/pmd/pmd.xml")
}

spotbugs {
    ignoreFailures = true
    effort = 'max'
    reportLevel = 'medium'
}

mainClassName = 'org.terasology.launcher.TerasologyLauncher'

def convertGitBranch = { gitBranch ->
    if (gitBranch != null) {
        // Remove "origin/" from "origin/develop"
        gitBranch.substring(gitBranch.lastIndexOf("/") + 1)
    } else {
        ""
    }
}

task createVersionInfoFile {
    inputs.property('version', version.toString())

    File versionInfoFileDir = new File(sourceSets.main.output.resourcesDir, 'org/terasology/launcher/')
    File versionFile = new File(versionInfoFileDir, 'version.txt')

    outputs.file(versionFile)

    doLast {
        versionInfoFileDir.mkdirs()
        versionFile.text = version
    }
}

task extractCodeMetricsConfig(type: Copy) {
    description = "Extract code metrics configuration to '$metricsConfigDir'"
    from {
        configurations.codeMetrics.collect {
            zipTree(it)
        }
    }
    into "$rootDir/$metricsConfigDir"
}

clean {
    delete createVersionInfoFile.outputs.files
    delete extractCodeMetricsConfig.destinationDir
}

processResources.dependsOn(createVersionInfoFile, extractCodeMetricsConfig)

jar {
    //TODO we only use this name because the `.exe` start scripts require the JAR to be named 'TerasologyLauncher.jar'
    jar.archiveFileName = "${project.name}.jar"
    // replace development "logback.xml" with productive "logback_jar.xml"
    exclude "logback.xml"
    rename('logback_jar.xml', 'logback.xml')
    manifest {
        def manifestClasspath = configurations.runtimeClasspath.collect { it.getName() }.join(" ")
        attributes("Main-Class": mainClassName)
        attributes("Class-Path": manifestClasspath)
        attributes("Implementation-Title": project.name)
        attributes("Implementation-Version": project.version)
        attributes("SplashScreen-Image": splashImage)

        // allow everything
        attributes('Permissions': 'all-permissions')
        attributes('Codebase': '*')
        attributes('Application-Name': project.name)
        attributes('Application-Library-Allowable-Codebase': '*')
        attributes('Caller-Allowable-Codebase': '*')
        attributes('Trusted-Only': 'false')
    }
}

task copyExtra(type: Copy) {
    filter(FixCrLfFilter, eol: FixCrLfFilter.CrLf.newInstance("crlf"))

    from('README.md') {
        rename('README.md', 'README.txt')
    }

    from('CHANGELOG.md') {
        rename('CHANGELOG.md', 'CHANGELOG.txt')
    }

    from('CONTRIBUTING.md') {
        rename('CONTRIBUTING.md', 'CONTRIBUTING.txt')
    }

    from('LICENSE')
    from('NOTICE')

    into "$buildDir/distributions"
}

task copyExtraIntoResources(type: Copy) {
    from('README.md')
    from('CHANGELOG.md')
    from('docs/CONTRIBUTING.md')
    from('LICENSE')

    into('src/main/resources/org/terasology/launcher/about')
}
processResources.dependsOn copyExtraIntoResources

task copyIconsIntoResources(type: Copy) {
    from('icons')

    into('src/main/resources/org/terasology/launcher/icons')
}
processResources.dependsOn copyIconsIntoResources

//TODO: Temporary workaround for new exes / JRE bundling not quite being officially ready yet
distZip.exclude("*.x*.exe")


idea {
    project.settings.delegateActions {
        delegateBuildRunToGradle = false
    }
}

tasks.named('wrapper') {
    // IDEA likes this distribution to better know things.
    distributionType = Wrapper.DistributionType.ALL
}
