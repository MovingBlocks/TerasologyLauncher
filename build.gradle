/*
 * This is a Gradle build file:
 * - Gradle Homepage: http://gradle.org/
 * - Gradle Documentation: http://gradle.org/documentation
 * - View tasks for this project: $ gradlew tasks
 */

apply plugin: 'java'
apply plugin: 'project-report'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'application'
apply plugin: 'checkstyle'

/*
 * Update Gradle Wrapper:
 * 1. Uncomment 'wrapper' task
 * 2. Change 'gradleVersion'
 * 3. Run "gradlew wrapper"
 * 4. Comment 'wrapper' task
 */
//task wrapper(type: Wrapper) {
//    gradleVersion = '1.4'
//}

import org.apache.tools.ant.filters.FixCrLfFilter;

// Declare "extra properties" (variables) for the project - a Gradle thing that makes them special.
ext {
    // Read environment variables, including variables passed by jenkins continuous integration server
    env = System.getenv()

    // Stuff for our automatic version file setup
    startDateTimeString = new Date().format("yyyy-MM-dd_HH-mm-ss")
    versionInfoFileDir = new File(sourceSets.main.output.resourcesDir, 'org/terasology/launcher/version')
    versionInfoFile = new File(versionInfoFileDir, 'versionInfo.properties')
    currentStableDisplayVersion = "0.2.0"
    displayVersion = (env.JOB_NAME == 'TerasologyLauncherStable') ? currentStableDisplayVersion : ((env.JOB_NAME != null && env.BUILD_ID != null) ? currentStableDisplayVersion + ' (' + env.JOB_NAME + ', ' + env.BUILD_ID + ')' : currentStableDisplayVersion + ' (local development)')

    // Splash image for the JAR
    splashImage = "org/terasology/launcher/images/splash.jpg"
}

// Declare remote repositories we're interested in - library files will be fetched from here
repositories {
    // Main Maven repo
    mavenCentral()
}

// Primary dependencies definition
dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.5'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.0.13'

    // These dependencies are only needed for running tests
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

// Set the expected module Java level (can use a higher Java to run, but should not use features from a higher Java)
sourceCompatibility = 1.6

mainClassName = "org.terasology.launcher.TerasologyLauncher"

test {
    // ignoreFailures: Specifies whether the build should break when the verifications performed by this task fail.
    ignoreFailures = true
    // showStandardStreams: makes the standard streams (err and out) visible at console when running tests
    testLogging.showStandardStreams = true
    jvmArgs '-Xms512m', '-Xmx1024m', '-XX:MaxPermSize=128m'
}

checkstyle {
    ignoreFailures = true
    configProperties.samedir = checkstyle.configFile.parentFile
}

task createVersionInfoFile {
    inputs.property('dateTime', startDateTimeString)
    onlyIf { env.BUILD_URL != null }
    doLast {
        versionInfoFileDir.mkdirs()
        ant.propertyfile(file: versionInfoFile) {
            ant.entry(key: 'buildNumber', value: env.BUILD_NUMBER)
            ant.entry(key: 'buildId', value: env.BUILD_ID)
            ant.entry(key: 'buildTag', value: env.BUILD_TAG)
            ant.entry(key: 'buildUrl', value: env.BUILD_URL)
            ant.entry(key: 'jobName', value: env.JOB_NAME)
            ant.entry(key: 'gitBranch', value: env.GIT_BRANCH)
            ant.entry(key: 'gitCommit', value: env.GIT_COMMIT)
            ant.entry(key: 'dateTime', value: startDateTimeString)
            ant.entry(key: 'displayVersion', value: displayVersion)
        }
    }
}
createVersionInfoFile.dependsOn processResources
jar.dependsOn createVersionInfoFile
distZip.dependsOn createVersionInfoFile
distTar.dependsOn createVersionInfoFile

jar {
    // replace development "logback.xml" with productive "logback_jar.xml"
    exclude "logback.xml"
    rename('logback_jar.xml', 'logback.xml')
    manifest {
        def manifestClasspath = configurations.runtime.collect { it.getName() }.join(" ")
        attributes("Main-Class": mainClassName, "Class-Path": manifestClasspath,
                "Implementation-Title": project.name, "Implementation-Version": displayVersion,
                "SplashScreen-Image": splashImage)
    }
}

applicationDistribution.from('README.md') {
    filter(FixCrLfFilter, eol: FixCrLfFilter.CrLf.newInstance("crlf"))
    rename('README.md', 'README')
}

applicationDistribution.from('CHANGELOG.md') {
    filter(FixCrLfFilter, eol: FixCrLfFilter.CrLf.newInstance("crlf"))
    rename('CHANGELOG.md', 'CHANGELOG')
}

applicationDistribution.from('LICENSE') {
    filter(FixCrLfFilter, eol: FixCrLfFilter.CrLf.newInstance("crlf"))
}

applicationDistribution.from('NOTICE') {
    filter(FixCrLfFilter, eol: FixCrLfFilter.CrLf.newInstance("crlf"))
}

applicationDistribution.from('src/template/VERSION') {
    expand(buildNumber: env.BUILD_NUMBER, buildUrl: env.BUILD_URL, gitBranch: env.GIT_BRANCH,
            dateTime: startDateTimeString, displayVersion: displayVersion)
    filter(FixCrLfFilter, eol: FixCrLfFilter.CrLf.newInstance("crlf"))
}

// For IntelliJ add a bunch of excluded directories
idea {
    // Exclude Gradle dir
    module.excludeDirs += file('gradle')

    // Exclude Eclipse dirs
    module.excludeDirs += file('bin')
    module.excludeDirs += file('.settings')
}
