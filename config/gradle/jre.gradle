/*
 * Copyright 2020 MovingBlocks
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Uses Bellsoft Liberica JRE
// https://bell-sw.com/pages/liberica_install_guide-11.0.7/
def jdkVersion = '11.0.7+10' // jre version haven't jmod (needed for jlink/jpackager)
def jreUrlFilenames = [
        Linux64   : 'linux-amd64-full.tar.gz',
//        Linux32   : 'linux-i586-full.tar.gz', // -full builds not available for linux-i586
        Windows64 : 'windows-amd64-full.zip',
        Windows32 : 'windows-i586-full.zip',
        Mac       : 'macos-amd64-full.zip'
]

task createRelease() {
    group 'Distribution'
    description 'Bundles the project with a JRE for each platform'
    dependsOn distZip

    doLast {
        println 'Created release: ' + version
    }
}

task downloadJreAll {
    group 'JRE'
    description 'Downloads Launcher JREs for all platforms'
}

task unpackJreAll {
    group 'JRE'
    description 'Unpack JREs for all platforms'
}


def createJreTasks(String taskNameBase,
                   String downloadUrl,
                   String downloadFile,
                   String unpackDir) {

    task "download$taskNameBase"(type: Download) {
        group 'JRE'
        src downloadUrl
        dest downloadFile
        overwrite false
    }

    def unpackTask = task "unpack$taskNameBase"(type: Copy) {
        group 'JRE'
        from(downloadFile.endsWith("zip")
                ? zipTree(downloadFile)
                : tarTree(downloadFile)) {
            eachFile { fcd ->
                fcd.relativePath = new RelativePath(
                        true, fcd.relativePath.segments.drop(1))
            }
            includeEmptyDirs = false
        }
        into unpackDir

        outputs.dir unpackDir

        dependsOn "download${taskNameBase}"
    }

    return unpackTask
}

jreUrlFilenames.each { os, file ->
    def launcherTaskBase = "Jre${os}"
    def unpackTask = createJreTasks(
            launcherTaskBase,
            "https://download.bell-sw.com/java/$jdkVersion/bellsoft-jdk$jdkVersion-$file",
            "$projectDir/jre/$os-$jdkVersion-$file",
            "$projectDir/jre/$os")

    distributions {
        def distName = os.toLowerCase()
        def distBase = distName.replaceAll("\\d", "") // drop '32' or '64'

        "$distName" {
            contents {
                with distributions.main.contents

                into('jre') {
                    from unpackTask
                }

                from("${projectDir}/buildres/${distBase}")
                from("${projectDir}/buildres/${distName}")
            }
        }
    }

    downloadJreAll.dependsOn "downolad${launcherTaskBase}"
    unpackJreAll.dependsOn "unpack${launcherTaskBase}"

    createRelease.dependsOn "assemble${os}Dist"
}

distributions {
    mac {
        contents {
            into 'TerasologyLauncher.app/Contents'
            exclude '**/*.bat'
            eachFile { details ->
                details.path = details.path.toString().replaceAll("(Contents)/bin/(.+)", "\$1/MacOS/\$2")
            }
        }
    }
}

/**
 * Patch start scripts to use bundled JRE. Patch files are located in 'buildres/scripts'.
 */
startScripts.doLast {
    ant.patch(
            originalfile: unixScript,
            patchfile: file("$projectDir/buildres/scripts/${file(unixScript).name}.patch")
    )

    ant.patch(
            originalfile: windowsScript,
            patchfile: file("$projectDir/buildres/scripts/${file(windowsScript).name}.patch")
    )
}
